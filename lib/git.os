		
Функция ПолучитьНазваниеТекущейВетки(КаталогРепозитария) Экспорт

	ПроцессГит = Создатьпроцесс("git rev-parse --abbrev-ref HEAD"
								,КаталогРепозитария
								,Истина
								,Ложь
								,КодировкаТекста.UTF8);
	ПроцессГит.Запустить();										
	ПроцессГит.ОжидатьЗавершения();
	
	НазваниеТекущейВетки  = СокрЛП(ПроцессГит.ПотокВывода.Прочитать());

	Возврат НазваниеТекущейВетки;

КонецФункции

Функция ПолучитьНазваниеВеткиСлияния() Экспорт

	ПроцессГит = Создатьпроцесс("git cherry -v HEAD MERGE_HEAD"
								,ТекущийКаталог()
								,Истина
								,Ложь
								,КодировкаТекста.UTF8);
	ПроцессГит.Запустить();										
	ПроцессГит.ОжидатьЗавершения();

	ТекстСообщения  = ПроцессГит.ПотокВывода.Прочитать();

	//Возврат СокрЛП(ТекстСообщения);
	Возврат "origin1c";
	
КонецФункции

Функция ОбработатьФайлИзменений(ПутьКРепозитарию, ИмяФайлаСпискаФайлов, Журнал) Экспорт
	
	ПутьКВременномуКаталогу = ОбъединитьПути(КаталогВременныхФайлов(),"1C_GIT_PART");
	СоздатьКаталог(ПутьКВременномуКаталогу);
	УдалитьФайлы(ПутьКВременномуКаталогу, "*.*");
	
	ИмяВрФайла = ПолучитьИмяВременногоФайла();

	// Добавить путь к репозитарию
	ЧТ = Новый ЧтениеТекста(ИмяФайлаСпискаФайлов, КодировкаТекста.UTF8);
	ЗТ = Новый ЗаписьТекста(ИмяВрФайла, КодировкаТекста.UTF8);

	Стр = ЧТ.ПрочитатьСтроку();
	СтрЗТ = "";

	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Новый");
	ТЗ.Колонки.Добавить("Путь");
	ТЗ.Колонки.Добавить("Уровень");

	Пока не Стр = неопределено Цикл
		
		Журнал.Отладка("Обрабатываю " + Стр);
		
		Если Не СокрЛП(Стр) = "" Тогда

			Стр = СтрРазделить(Стр, Символы.Таб);
		
			СтрЗТ = "" + ПутьКРепозитарию + "\" + СтрЗаменить(Стр[1],"/","\");
			
		Иначе

			СтрЗТ = "";
		
		КонецЕсли;
		
		Если Стр[0] = "R088" ИЛИ СтрНайти(Стр[1], "Config/") = 0 ИЛИ СтрНайти(Стр[1], "Config/ConfigDumpInfo.xml") <> 0 Тогда
		
			Журнал.Информация("Пропускаю  " + Стр[0] + "	" + Стр[1]);
			Стр = ЧТ.ПрочитатьСтроку();
			Продолжить;
		
		КонецЕсли;
		
		БылоПреобразование = Ложь;
		
		ПозExt = СтрНайти(СтрЗТ, "\Ext\");
		Если ПозExt > 0 Тогда
		
			СтрЗТ = Сред(СтрЗТ, 1, ПозExt - 1) + ".xml";
			БылоПреобразование = Истина;
			
		КонецЕсли;		
		
		Если СтрНайти(СтрЗТ, "Config.xml") > 0 Тогда
		
			СтрЗТ = "Config\Configuration.xml";
		
		КонецЕсли;			
		
		Если БылоПреобразование Тогда
		
			Журнал.Информация("Преобразовано: " + ПутьКРепозитарию + "\" + СтрЗаменить(Стр[1],"/","\") + " -> " + СтрЗТ);
			
		КонецЕсли;

		Если СтрНайти(СтрЗТ, "\Config") > 0 И Не СокрЛП(СтрЗТ) = "" Тогда
		
			Если (Стр[0] = "A" или Стр[0] = "D") и СтрЗаканчиваетсяНа(СтрЗТ, "xml") Тогда
		
				МасСтр = СтрРазделить(СтрЗТ, "\");
				МасСтр.Удалить(МасСтр.Количество() - 1);
				МасСтр.Удалить(МасСтр.Количество() - 1);
				
				СтрРодитель = СтрСоединить(МасСтр, "\") + ".xml";
				
				СтрРодитель = СтрЗаменить(СтрРодитель, "Config.xml","Config\Configuration.xml");
	
				СтрТз= ТЗ.Добавить();
				СтрТз.Путь = СтрРодитель;
				СтрТз.Уровень = СтрЧислоВхождений(СтрРодитель, "\");
			
				Журнал.Информация(Стр[0] + " " + СтрЗТ + " -> " + СтрРодитель);
			
			КонецЕсли;
		
			Если не Стр[0] = "D" Тогда
			
				СтрТз= ТЗ.Добавить();
				СтрТз.Путь = СтрЗТ;
				СтрТз.Уровень = СтрЧислоВхождений(СтрЗТ, "\");
			
			КонецЕсли;
		
		КонецЕсли;
		
		Стр = ЧТ.ПрочитатьСтроку();
		
	КонецЦикла; 

	ТЗ.Сортировать("Уровень, Путь");
	ТЗ.Свернуть("Путь");

	Для каждого СтрТз из ТЗ Цикл
		
		ПутьКРезультатуКопирования = СтрЗаменить(СтрТз.Путь,ПутьКРепозитарию,ПутьКВременномуКаталогу);
		Журнал.Информация("Копирую во временный каталог: " + СтрТз.Путь + " в " + ПутьКРезультатуКопирования);
		Файл = Новый Файл(ПутьКРезультатуКопирования);
		СоздатьКаталог(Файл.Путь);
		ИсходныйФайл = Новый Файл(СтрТз.Путь);
		Если ИсходныйФайл.Существует() Тогда
			КопироватьФайл(СтрТз.Путь, ПутьКРезультатуКопирования);
		КонецЕсли;
		ЗТ.ЗаписатьСтроку(ПутьКРезультатуКопирования);
		
		
		ПутьExt = ОбъединитьПути(ИсходныйФайл.Путь, ИсходныйФайл.ИмяБезРасширения , "Ext");
		КаталогExt = Новый Файл(ПутьExt);
		Если КаталогExt.Существует() Тогда
			МасФайлов = НайтиФайлы(ПутьExt, "*.*", Истина);
			Для Каждого ФайлExt Из МасФайлов Цикл
				Если ФайлExt.Существует() И не ФайлExt.ЭтоКаталог() Тогда
					ПутьКРезультатуКопирования = СтрЗаменить(ФайлExt.ПолноеИмя,ПутьКРепозитарию,ПутьКВременномуКаталогу);
					Файл = Новый Файл(ПутьКРезультатуКопирования);
					СоздатьКаталог(Файл.Путь);
					Журнал.Информация("Копирую во временный каталог: " + ФайлExt.ПолноеИмя + " в " + ПутьКРезультатуКопирования);
					КопироватьФайл(ФайлExt.ПолноеИмя, ПутьКРезультатуКопирования);
				КонецЕсли;	
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;

	ЗТ.Закрыть();
	ЧТ.Закрыть();

	УдалитьФайлы(ИмяФайлаСпискаФайлов);
	ПереместитьФайл(ИмяВрФайла, ИмяФайлаСпискаФайлов);

	ПутьКРепозитарию = ПутьКВременномуКаталогу;
	
	Возврат ТЗ.Количество();
	
КонецФункции

Процедура ПерейтиНаВетку(ПутьКРепозитарию, ИмяВетки, Журнал = неопределено) Экспорт

	ТекущаяВетка = ПолучитьНазваниеТекущейВетки(ПутьКРепозитарию);

	// Проверить текущую ветку и если она не соответствует, перейти на нее
	Если Не ТекущаяВетка = ИмяВетки Тогда
	
		ЗапуститьПриложение("git checkout " + ИмяВетки, ПутьКРепозитарию, Истина);
		
	ИначеЕсли Не Журнал = неопределено Тогда
		
		Журнал.Информация("переход на ветку " + ИмяВетки + " так как текущая ветка и так " + ТекущаяВетка);
		
	КонецЕсли;

КонецПроцедуры

Процедура ПолучитьСписокИзмененийВФайл(Знач ПутьКРепозитарию, Знач ИмяФайлаСпискаФайлов, Журнал, СравниваемыеКомиты = Неопределено) Экспорт
	
	Если не СравниваемыеКомиты = Неопределено Тогда
		Сообщить("Запускаю: cmd /C git diff " + СравниваемыеКомиты[0] + " " + СравниваемыеКомиты[1] + " --name-status > " + ИмяФайлаСпискаФайлов);
		ЗапуститьПриложение("cmd /C git diff " + СравниваемыеКомиты[0] + " " + СравниваемыеКомиты[1] + " --name-status > " + ИмяФайлаСпискаФайлов, ПутьКРепозитарию, Истина);
	
	Иначе
		
		ЗапуститьПриложение("cmd /C git diff @{1}.. --name-status > " + ИмяФайлаСпискаФайлов, ПутьКРепозитарию, Истина);
		//ЗапуститьПриложение("cmd /C git diff d1315564988df781c8d8e88245564284cc82e7bf f43dc75005c330be4c8f88b9df87593da94e07eb --name-status > " + ИмяФайлаСпискаФайлов, ПутьКРепозитарию, Истина);
		
	КонецЕсли;	
	
	
КонецПроцедуры
